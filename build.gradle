allprojects {
    group 'de.exlll'
    version '1.4.0'
}
subprojects {
    apply plugin: 'java'

    repositories { mavenCentral() }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'com.google.jimfs', name: 'jimfs', version: '1.1'
    }
}
buildscript {
    if (project.hasProperty("local_script")) {
        apply from: file(local_script + "/buildscript.local.gradle"), to: buildscript
    }
}
if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

project(':configlib-core') {
    dependencies { compile group: 'org.yaml', name: 'snakeyaml', version: '1.17' }
}
project(':configlib-bukkit') {
    repositories { maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' } }
    dependencies {
        compile project(':configlib-core')
        compile group: 'org.bukkit', name: 'bukkit', version: '1.11.2-R0.1-SNAPSHOT'
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
}
project(':configlib-bungee') {
    repositories { maven { url 'https://oss.sonatype.org/content/repositories/snapshots' } }
    dependencies {
        compile project(':configlib-core')
        compile group: 'net.md-5', name: 'bungeecord-api', version: '1.10-SNAPSHOT'
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
}
project(':configlib-forge') {
    buildscript {
        repositories {
            maven { url = "http://files.minecraftforge.net/maven" }
        }
        dependencies {
            classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        }
    }
    dependencies {
        compile project(':configlib-core')
    }
    jar { from { project(':configlib-core').sourceSets.main.output } }
}